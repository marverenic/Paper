apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.targetSdkVersion
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.marverenic.reader"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }

    dataBinding {
        enabled true
    }

    buildTypes {
        def apiKeysFile = rootProject.file("feedly.properties")
        def apiKeys = new Properties()
        if (apiKeysFile.exists()) apiKeys.load(new FileInputStream(apiKeysFile))

        debug {
            buildConfigField 'String', 'DEV_USER_ID', apiKeys.getProperty('feedlyDevUserId', 'null')
            buildConfigField 'String', 'DEV_OAUTH_TOKEN', apiKeys.getProperty('feedlyDevOauthToken', 'null')
            buildConfigField 'String', 'CLIENT_ID', apiKeys.getProperty('feedlyClientId', 'null')
            buildConfigField 'String', 'CLIENT_SECRET', apiKeys.getProperty('feedlyClientSecret', 'null')
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String', 'DEV_USER_ID', 'null'
            buildConfigField 'String', 'DEV_OAUTH_TOKEN', 'null'
            buildConfigField 'String', 'CLIENT_ID', apiKeys.getProperty('feedlyClientId', 'null')
            buildConfigField 'String', 'CLIENT_SECRET', apiKeys.getProperty('feedlyClientSecret', 'null')
        }
    }
}

kapt {
    generateStubs true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation "com.android.support:appcompat-v7:$rootProject.supportLibVersion"
    implementation "com.android.support:design:$rootProject.supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    kapt "com.android.databinding:compiler:$gradle_plugin_version"

    implementation 'com.marverenic.heterogeneousadapter:heterogeneousadapter:1.1'

    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$rootProject.retrofitVersion"
    implementation "com.squareup.moshi:moshi:1.5.0"

    implementation "io.reactivex.rxjava2:rxjava:2.1.4"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-kotlin:2.1.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-android:2.1.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'

    implementation 'com.google.dagger:dagger:2.10'
    kapt 'com.google.dagger:dagger-compiler:2.10'

    implementation 'net.danlew:android.joda:2.9.9'
}
